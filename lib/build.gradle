// Specify module plugins using new 2.1+ syntax to avoid modifying
// projects depending on this module. Plugins must appear at the start of
// the file.
plugins {
    id 'ch.netzwerg.release' version '1.1.0'
}

apply plugin: 'java'
apply from: '../deploy.gradle'
apply from: '../config/quality.gradle'

group = 'org.mediawiki.api'
archivesBaseName = 'json'

dependencies {
    compile 'org.json:json:20140107'
    compile 'com.github.kevinsawicki:http-request:5.6'
    testCompile 'junit:junit:4.12'
}

final JAVA_VERSION = JavaVersion.VERSION_1_7
if (JavaVersion.current() != JAVA_VERSION) {
    throw new IllegalStateException("Incorrect Java Development Kit version; expected "
            + JAVA_VERSION + " but found " + JavaVersion.current())
}
compileJava {
    sourceCompatibility = JAVA_VERSION
    targetCompatibility = JAVA_VERSION
}

release {
    dependsOn = [uploadArchives]
    tagPrefix = 'r/'
}
